#!/usr/bin/env nu

def "refresh workspaces" [] {
	hyprctl workspaces -j
	| from json
	| where name !~ :scratch
	| sort-by id
	| to json --raw
	| update workspaces $in

	hyprctl activeworkspace -j
	| update workspace $in
}

def "refresh window" [event: record] {
	hyprctl clients -j
	| from json
	| where address == $"0x($event.data)"
	| $in ++ [{}]
	| first
	| to json --raw
	| update window $in
}

def process [line: string] {
	let event = $line | parse "{type}>>{data}" | first
	match $event.type {
		monitoradded | monitorremoved => {
			# TODO: fix
			bars
		},
		activewindowv2 => {
			refresh window $event
		},
		createworkspace => {
			match $event.data {
				"0" => { "lab" },
				"1" => { "web" },
				"2" => { "dev" },
				"3" => { "remote" },
				"4" => { "hack" },
				"5" => { "lit" },
				"6" => { "media" },
				"7" => { "music" },
				"8" => { "monitor" },
				"9" => { "files" },
				"10" => { "chat" },
				"11" => { "design" },
				"12" => { "games" },
				_ => { "" },
			} | hyprctl dispatch renameworkspace $event.data $in
		},
		workspace | createworkspace | destroyworkspace | renameworkspace | focusedmon => {
			refresh workspaces
		},
		submap => {
			update mode (if $event.data == "" { "NORMAL" } else { $event.data })
		},
		urgent => {
			# TODO
			notify-send urgent $event.data
		},
	}
}

def update [key: string, value: string] {
	eww update $"($key)=($value)"
}

def init [] {
	refresh workspaces
	hyprctl activewindow -j | update window $in
	update mode NORMAL
}


def main [] {}

def "main listen" [] {
	init
	socat -u $"UNIX:/tmp/hypr/($env.HYPRLAND_INSTANCE_SIGNATURE)/.socket2.sock" -
	| lines -s
	| each {|line|
		process $line
	}
}

def "main change workspace" [monitor: string, direction: string] {
	let current = hyprctl activeworkspace -j
	| from json
	| get id

	let workspaces = hyprctl workspaces -j
	| from json
	| where name !~ :scratch and monitor == $monitor
	| sort-by id

	match $direction {
		up => {
			if ($workspaces | where id < $current | is-empty | not $in) {
				hyprctl dispatch workspace m-1
			}
		},
		down => {
			if ($workspaces | where id > $current | is-empty | not $in) {
				hyprctl dispatch workspace m+1
			}
		},
	}
}
